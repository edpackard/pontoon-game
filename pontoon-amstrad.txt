1 REM PONTOON
10 REM INITIALISE
20 YC=2:CC=2
30 ACES=0
40 CACES=0
50 S=0
60 T=0
70 DIM SUIT$(4)
80 SUIT$(1)="CLUBS"
90 SUIT$(2)="HEARTS"
100 SUIT$(3)="SPADES"
110 SUIT$(4)="DIAMONDS"
120 CLS
130 DIM PACK(52) 
140 FOR X=1 TO 52
150 PACK (X)=0
160 NEXT X
170 REM DEAL TWO CARDS TO EACH PLAYER
180 LOCATE 10,3
190 PRINT"YOU";SPC(15);"HOUSE"
200 LOCATE 3,5
210 GOSUB 740
220 S=S+F
230 IF F=11 THEN ACES=ACES+1
240 LOCATE 3,6
250 GOSUB 740
260 S=S+F
270 IF F=11 THEN ACES=ACES+1
280 LOCATE 24,5
290 GOSUB 740
300 T=T+F
310 IF F=11 THEN CACES=CACES+1
320 LOCATE 24,6
330 GOSUB 740
340 T=T+F
350 IF F=11 THEN CACES=CACES+1
360 REM INPUT OPTION - TWIST (T) OR STICK (S)
370 X$=INKEY$:IF X$<>"S" AND X$<>"T" THEN 370
380 IF X$="S" THEN 560
390 LOCATE 3,YC+5
400 YC=YC+1
410 GOSUB 740
420 S=S+F
430 IF F=11 THEN ACES=ACES+1 
440 REM CHECK SCORE AND ACES
450 IF S<22 THEN 370
460 IF ACES = 0 THEN 500
470 ACES = ACES-1
480 S = S-10
490 GOTO 450
500 LOCATE 12,19
510 PRINT "YOU'RE BUST"
520 PRINT "ANOTHER GAME (Y/N)"
530 X$=INKEY$:IF X$<>"Y" AND X$<>"N" THEN 530
540 IF X$="Y" THEN RUN
550 END
560 IF T>16 THEN GOTO 700
570 CC=CC+1
580 LOCATE 24,CC+4
590 GOSUB 740
600 T=T+F
610 IF F=11 THEN CACES=CACES+1
620 IF T<21 THEN 560
630 IF CACES = 0 THEN 670
640 CACES = CACES-1
650 T=T-10
660 GOTO 620
670 LOCATE 12,19
680 PRINT "YOU WIN!"
690 GOTO 520
700 LOCATE 12,19
710 IF T<S THEN 680
720 PRINT "THE HOUSE WINS"
730 GOTO 520
740 REM DEAL CARD
750 LET CARD=INT ( RND (1)*52+1 )
760 IF PACK(CARD)=1 THEN GOTO 750
770 PACK(CARD)=1
780 F=CARD-13*INT(CARD/13)
790 IF F=0 THEN F=13
800 IF F=1 OR F>10 THEN GOTO 850
810 PRINT F;"OF ";
820 IF F>10 THEN F=10
830 PRINT SUIT$(INT((CARD-1)/13)+1)
840 RETURN
850 IF F=11 THEN PRINT "JACK OF  ";
860 IF F=12 THEN PRINT "QUEEN OF ";
870 IF F=13 THEN PRINT "KING OF  ";
880 IF F<>1 THEN GOTO 820
890 F = 11
900 PRINT "ACE OF   ";
910 GOTO 830